if(NOT TARGET costa::costa)
    cmake_policy(PUSH) # Save project's policies
    if(POLICY CMP0074)
        cmake_policy(SET CMP0074 NEW)
    endif()
    include(CMakeFindDependencyMacro)

    # Bundled modules should be found first to prevent conflicts with similarly 
    # named modules in calling projects. 
    #
    set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR} ${CMAKE_MODULE_PATH})

    set(MKL_ROOT "@MKL_ROOT@")
    set(MPI_CXX_COMPILER "@MPI_CXX_COMPILER@")

    find_package(MPI)

    set(COSTA_SCALAPACK "@COSTA_SCALAPACK@")

    if (${COSTA_SCALAPACK} STREQUAL "MKL")
        find_package(MKL)
    elseif (${COSTA_SCALAPACK} STREQUAL "CRAY_LIBSCI")
        find_package(CRAY_LIBSCI REQUIRED)
    elseif (${COSTA_SCALAPACK} STREQUAL "CUSTOM")
        find_package(SCALAPACK)
    endif ()

    if ("@COSTA_WITH_PROFILING@")
        find_dependency(semiprof)
    endif ()

    # Clean-up module path.
    #
    list(REMOVE_ITEM CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

    # These are bundled with costa
    #
    find_dependency(grid2grid)

    include("${CMAKE_CURRENT_LIST_DIR}/costaTargets.cmake")
    cmake_policy(POP) # Restore project's policies
endif()
